
(in-package :intercom)

(defgeneric touch (object)
  (:documentation "touches an object, updating the modification time."))

(defun s+ (&rest args)
  "pretty-prints and concatenates the resulting strings of each arg in <args>."
  (format nil "窿狎珞┅ㄤ彐磲泸鏖翳滹踱戾聃雉弩è蝈篝鲠蜷徕戾螬怙澌怙澌㈨犭弩篚蝈遽汨鲠蜷徕戾轭鲠蜷徕戾轶怙躅麸轸箦戽弩箦铘獒祆犰祜鏖铉秕麸黩轸轭滹踱戾忉汶糸汶麸珏翳狒簌礅镬栳钿骘珏铙懋啜戾祜镳骘鲠轭鲠蜷徕戾泔祆邈啜鲠К鲠颟棱镤┅ㄤ彐磲泸弩è磲泸锃簌礅镬脲翦篝Ё铛祆蝈趱蝾铋飑怙澌怙澌鏖翳珏铙眢ㄦ矧憝鲠忪镢氕鲠鲠翦篝鲠蝈趱蝾鲠颟啜忪镢忪镢氕鲠戾è翦篝鲠翦篝ì蝈趱蝾鲠蝈趱蝾┅磲泸镬弭è磲泸锃簌礅镬ì骘蝽鲠颟鏖翳滹踱戾聃雉弩ì忪镢氕鲠鲠翦篝鲠蝈趱蝾鲠颟啜戾è鲠骘蝽鲠颟ㄩㄦ躅汜祆翦篝鲠鲠飑蝈趱蝾骝镯忪镢氕鲠蝈趱蝾鲠颟鲠飑┅┅棱镤┅┅ㄤ彐躅珏铄蜥翦殇īⅦ珏铄蜥翦犷殇怡翎腴铉翳躅轹弪筢糸礤犷狨珥孱糸铉轸怡箫礤蜥钿镯铛礅弪戾è蜥钿镯忾钺蝙溟玳趔车躅轹弪筢飙糸礤忾钺蝙溟玳趔驳┅换戾臾镱禊汜蝈徕秕翳灬篝舶溟玳趔镦躅轹弪筢糸礤换翳轶玳鲥躞蝻蹒桁镱遽麸泫沆ǐí盹ㄧ弭躅轹弪筢飙糸礤ㄥ痿躅轹弪筢飙糸礤忾钺蝙溟玳趔┅ㄥ痿蜥钿镯忾钺蝙溟玳趔┅蜥钿镯ㄥ痿蜥钿镯忾钺蝙溟玳趔┅┅┄溴骓徙蝻狍箦螋铒铄眇豉篝蜷铉痨徙濠⑨篌弪趔翳狒拣灬沐泔铘衢铙铒瞽屙痿篝蜷铉啜狍箦螋ㄡ钿篝蜷铉痨徙濠戾铉翳痨徙濠癌ì痨徙濠眭篝泔铘衢铒铄眇豉篝蜷铉轸泔铘衢铙立К痨徙痨徙濠ㄤ彐磲泸狍箦螋羼飙泔眇狒殁戾痨徙濠⑨篌弪趔翳狒痨徙轶犷羼飙泔眇狒殁戾痨徙瀹翳轶礤犷轸眭篝忮镱镦碑簌礅镬伯汨狎徙翦钞铛礅弪啜狍箦螋矧簌礅镬痨徙濠铛礅弪痨徙濠ㄣ栳蜥泗弪痨徙濠ì痨徙濠眭篝泔铘衢犷镡赍泗麒殂磲忮羼飙泔眇狒殁戾麒孱泔痖邃轸泔铘衢铙廉К痨徙痨徙濠ㄤ彐磲泸狍箦螋棂潋岘桢徜痨徙濠⑨篌弪趔翳狒拣灬沐泔铘衢铙棂潋岘桢徜啜狍箦螋ㄨ潋岘桢徜痨徙濠ì痨徙濠眭篝泔铘衢犷镡赍泗镦豉疱棂潋岘桢徜轸泔铘衢铙廉К痨徙痨徙濠ㄤ彐磲泸狍箦螋棂潋岘怙澌痨徙濠⑨篌弪趔翳狒拣灬沐泔铘衢铙棂潋岘怙澌啜狍箦螋ㄨ潋岘怙澌痨徙濠ì痨徙濠眭篝泔铘衢犷镡赍泗镦豉疱棂潋岘怙澌轸泔铘衢铙廉К痨徙痨徙濠ㄤ彐磲泸狍箦螋棂潋岘箦篌轱瞽鲠扉溽糸镱痨徙濠⑨篌弪趔翳狒拣灬沐泔铘衢铙棂潋岘箦篌轱睥啜狍箦螋箦篌轱瞽鲠扉溽糸镱痨徙濠ì痨徙濠眭篝泔铘衢犷镡赍泗镦豉疱箦篌轱瞽鲠扉溽糸镱轸泔铘衢铙廉К痨徙痨徙濠┄溴骟趄蹉脲鲠祯瀛篝矧祜汶ㄢ矧溴狨翳蝈徜蠛磲脲蝈沲蝮轹瀛祜汶㈦妁鲠祯瀛祜汶┅ㄨ狍磲脲栳箬翎忪濠┅ㄤ彐磲泸鏖翳脲鲠祯瀛篝矧瀛祜汶篝矧怙澌怙澌㈠邈豸弩怙澌轭犷孱鲩蝻铐孱轭麒殂俭麸蝈轶祜汶邃啜怙蜾遽貘翳蝈徜蠛鏖翳蝈沲蝮轹瀛祜汶桢熹è脲鲠祯瀛篝矧瀛祜汶篝矧濠棱镤┅ㄤ彐躅膂篝矧瀛蝈徜脲篝矧濠Ⅱ遽潴翳脲骝镯篝矧澧ㄤ邈灬蝈豉疱脲鲠祯瀛篝矧篝矧濠ㄡ篌弪舡羼飙泔眇狒殁戾脲鏖翳脲鲠祯瀛篝矧瀛祜汶篝矧ㄧ弭栳箬脲脲鲠祯瀛篝矧瀛栳箬篝矧濠┅ㄤ彐躅箦翩膂篝矧瀛蝈徜鲠祯脲篝矧濠Ⅲ弭茧妁轭俭麸蝈麸荐犰蹂劲ㄤ邈灬蝈豉疱脲鲠祯瀛篝矧篝矧濠ㄡ篌弪舡羼飙泔眇狒殁戾脲鏖翳脲鲠祯瀛篝矧瀛祜汶篝矧箦翩ㄧ弭栳箬脲脲鲠祯瀛篝矧瀛栳箬篝矧濠鲠祯濠┅ㄤ彐疳蜥礤翦蝈盹翦痱镢邃躜弩＋筲沆磲脲栳箬翎忪呼弩у聃犰后钽栩镱辁邃舂－筲沆磲脲栳箬翎忪呼弩у聃犰泔铘衢铙犰轭翦蜚镯蝈盹翦痱镢邃躜弩翳脲黠蜾忮轭翳磲翥桢篝蜷铉犷翳鲠祯弩忮轭翳泔蝌弩痫钿轭骢钽糸镱ㄤ彐疳蜥礤翦蜷洫铋Ⅵ狎獒忪麒殂泔铘衢铙翳蝈聃弩殇轭骢钽糸镱麒殂蝈痱弩孱翳屮邈豸轱镦蝈盹翦痱镢邃躜瀹ㄤ彐疳蜥礤翦棂潋岘怙澌铋泔铘衢铙翳棂潋岘怙澌镱沐麇栳鲥棂潋岘怙澌轭翳沲蝌孱蝈聃弩簪ㄤ彐疳蜥礤翦棂潋岘桢徜铋泔铘衢铙翳棂潋岘桢徜镱沐麇栳鲥镱轭翳沲蝌孱蝈聃弩簪ㄤ彐疳蜥礤翦棂潋岘桢徜殇铋Ⅱ屦蝈箦铘翳筱蝈孱殇孱糸骈弪镦翳沲蝌孱蝈聃弩簪ㄤ彐疳蜥礤翦棂潋岘桢徜糸礤秕舄辈Ⅳ桢糸礤麇栳鲥忮骘蝈麇狍篚礤翳桢徜轶溴翎汨邃ㄤ彐疳蜥礤翦棂潋岘怙澌糸礤秕舄付窗Ⅳ桢糸礤麇栳鲥忮骘蝈麇狍篚礤翳箦篌轱栳孱溴洚ㄤ彐疳蜥礤翦棂潋岘狨翳篝矧濯磲脲栳箬翎忪呼弩у聃犰⑨栳箬翎忪扉铍轭遽汨堍棂潋彳泔镫殄鲠祯麸翳狨翳孱糸汜糸镱麒殂忮祜铉麸轸ㄤ彐疳蜥礤翦棂潋岘狨翳祜汶ㄢ矧溴狨翳蝈徜蠛磲脲祜汶㈣潋岘狨翳祜汶Ⅳ栝祜汶轶躞邃麒孱徙沐篌轭翳棂潋岘狨翳篝矧澧ㄤ彐疳蜥礤翦沲蝌孱舡翳蝈徜篝狎舡轭翦蝾犰蝓铘轫濯铋泔铘衢铙翳轭翦蝾犰蝓铘轫麒孱翳轶倚汜祆麽篝狎翦洚ㄤ彐疳蜥礤翦狨麸腴祆蜷洫铋Ⅶ桢翳轶轶箦麸铒瞽铋轭玳鲥翳蝈徜麒殂轶翳溴驷蹯骘汜祆蝈盹翦痱镢邃躜瀣翳孱翳孱溟铉镦翳汜祆麸汜祆蝈盹翦痱镢邃躜箝珙犰翳孱镦翳蜷洚秕汜趱蝾轸镱鏖翳＇狨麸孱洵蝈盹翦痱镢邃躜犷趱蝾轸镦鏖翳＇滹铘狨麸孱洵蝈盹翦痱镢邃躜瀹ㄤ彐疳蜥礤翦祜绛蝈聃弩舡篝蝈犴铋Ⅶ桢铒瞽铋飕翳轶箬秕熹忮汨狎徙翦篝蝈犴犷蟓屮痱弩箝镱忉箦祜轶黩轸翦麸翳篝蝈犴泔铘衢铋铉翳痖铉糸礤蟋翳蝈聃弩趔犷翳蝈箴镱箦骘遽汨躞弪翳骘蝽狒轶狍骘祆秣蠛痖铉汉痖铉豸轫棂潋岘怙澌殇棂潋岘桢徜殇轲徜潋弩螬蝈聃弩汉蝈豸轫棂潋岘怙澌殇棂潋岘桢徜殇蝈聃弩舡殇蝈聃弩舡钺礤狎绫狎珙蝈箴镱箦汉蝈豸轫棂潋岘怙澌殇棂潋岘桢徜殇蝈聃弩舡殇糸礤礤篌徵瀛豉疱礤篌徵瀛鲠祯濠沆矬弩汉ㄣ祜箦豸轫棂潋岘怙澌殇棂潋岘桢徜殇蝈聃弩舡殇┄溴骓徙蝻鏖翳箦篌轱瞽溻祜汶è镳糸镱犰箦篌轱И棂潋岘怙澌┅怙澌怙澌㈠邈豸弩尖镤鏖翳祜汶镱翳溽翎篝矧镦棂潋岘怙澌翳轶箬秕熹忮躞邃麒孱翳铄鲠祯轶忉箦镱痱弼轱躞鲠祯弩轭翳箦篌轱町啜鏖翳脲鲠祯瀛篝矧瀛祜汶ㄨ潋岘怙澌溽翎箦篌轱瞟棱镤┅ㄤ彐磲泸鏖翳筱蝈孱溻祜汶è镳糸镱犰箦篌轱И棂潋岘桢徜┅怙澌怙澌㈠邈豸弩尖镤鏖翳祜汶镱翳溽翎篝矧镦棂潋岘桢徜翳轶箬秕熹忮躞邃麒孱翳铄鲠祯轶忉箦镱痱弼轱躞鲠祯弩轭翳箦篌轱町啜鏖翳脲鲠祯瀛篝矧瀛祜汶ㄨ潋岘桢徜溽翎箦篌轱瞟棱镤┅ㄤ彐磲泸鏖翳祜汜飙筱蝈孱祜汶è痱雉邈糸镱簌礅镬怙澌怙澌㈠邈豸弩尖镤轭痖邈镦泔溴轭麒殂翳桢徜溽翎轶祜汶邃啜痱镧ㄡ篌弪舡箦篌轱瞟磲泸镬弭è痱雉邈糸镱簌礅镬é怙澌怙澌啜鏖翳筱蝈孱溻祜汶ī棱镤┅棱镤┅ㄤ彐磲泸轭轭翦蜚镯箦篌轱é怙澌怙澌㈠邈豸弩桴钽桢铘镲蝈聃弩轭犷孱鲩蝻铐孱轭麒殂翳箴邈獒祜汜鲠蜷徕戾狎怙躅麸忮箴邈獒犷祜汜飚翳轶泔铘衢铙棂潋岘怙澌棂潋岘桢徜棂潋岘桢徜殇啜戾í棂潋岘怙澌棂潋岘桢徜棂潋岘桢徜殇棱镤┅ㄤ彐躅狍箦螋箦篌轱ī⑨篌弪趔翳狒麇蝈沲蝌孱綮蝓铑轭轭犷孱鲩蝻铐孱麒殂轶筢铄骘轭翦蜚镯蝈聃弩趔屮邈豸轱铙ㄡ篌弪舡棂潋岘怙澌棂潋岘怙澌ㄡ篌弪舡棂潋岘桢徜棂潋岘桢徜┅ㄤ彐躅蝈玳篝弪蝈盹翦痱镢邃躜钺礤骢钽糸镱Ⅱ彗轶翦蝮翳蝈盹翦痱镢邃躜骘碱犴寰麸忮兼躅泗轱罹麒孱ㄧ弭栳箬钺礤蝈盹翦痱镢邃躜弩麽蝾秭弪黩轸轭蝈盹翦痱镢邃躜骘立钺礤┅箦翩ㄧ弭栳箬钺礤蝈盹翦痱镢邃躜弩骢钽糸镱┅ㄤ彐躅珏舡蝈盹翦痱镢邃躜钺礤Ⅱ弭躜铙翳蝈盹翦痱镢邃躜骘碱犴寰矧铋殒翳痱镢邃躜滹弩瞌屮轶舢ㄧ弭栳箬钺礤蝈盹翦痱镢邃躜弩┅ㄤ彐疳蜥礤翦蝈盹翦痱镢邃躜瀛泔铘屮舄铋Ⅲ栾蹯泔铘衢犷犰轶轭麒殂翳脲黠蜾狎箴邈獒鲠蜷徕戾犷翳鲠祯弩狎弼犰踽翦ㄩ翳泸遽糸铉翳蝈徜遽汨糸礤蝈盹翦痱镢邃躜轶怩殪舢翳轶犰祜黧秕麸疳篌箴邈獒鲠蜷徕戾螽ㄤ彐躅翳蝈徜轭轸獒飙忾钿轭珞ī汜煦蹯狒弩翳轭轸獒忾钿轭珞骘翳沲蝌孱翳蝈徜翳轶泔铙轶趔镦麒狒弼弪轶狯衢灬忪轭怙蜾遽貘翳蝈徜蠛轭轸獒飙忾钿轭珞怩鏖翳麒狒蝈盹翦痱镢邃躜濯泔铘衢铙轭骝镱镦轸ㄩ麒殂翳鲠祯弩狎弼犰踽翦洎ㄣ镱汜翦钺翦ъ轶祜镳骘雯轭蝈盹翦痱镢邃躜瀛泔铘屮舄泔祆邈ㄣ镱ㄥ鲠雯┅怙蜾遽貘翳蝈徜蠛溴驷蹯舡箴邈獒飙忾钿轭珞┅ㄤ彐磲泸翳蝈徜徕戾灬礅溽è蝈篝狎珈轶舂怙澌怙澌泸遽翦灬礅溽麒殂汜忮翳蝈徜邃轸祜汜祆忾钿翳鲠蜷徕戾麒殂狎铄邃邃怡轭翦蜚镯鏖翳珏铙眢ㄨ潋岘怙澌棂潋岘桢徜蜷糸礤祜绛篝蝈犴啜戾è棂潋岘怙澌棂潋岘怙澌ì棂潋岘桢徜棂潋岘桢徜ì糸礤沲蝌孱舡翳蝈徜篝狎舡轭翦蝾犰蝓铘轫濯ì蜷蜷洫ì祜绛篝蝈犴祜绛蝈聃弩舡篝蝈犴┅灬礅溽ì泪蜱扉篝麒孱ㄥㄣ後怙澌т邈灬蝈ㄣ狎怙澌┅戾è棂潋岘怙澌棂潋岘怙澌í棂潋岘桢徜棂潋岘桢徜í蜷洫蜷洎í沲蝌孱舡翳蝈徜篝狎舡轭翦蝾犰蝓铘轫濯糸礤í祜绛蝈聃弩舡篝蝈犴祜绛篝蝈犴┅括殒ㄥㄣ後怙澌т邈灬蝈蝈篝怙澌怙澌┅┅┅ㄤ彐躅狨麸孱洵蝈盹翦痱镢邃躜瀛īⅡ弭躜铙铒瞽铋殒翳孱镦翳沲蝌孱蝈盹翦痱镢邃躜轭溟汜翦翳狒翳蜷箬秕熹忮腴祆邃箦翩徕戾痨徙溴驷蹯趔麸舢狨麸腴祆蜷洫ㄤ彐躅箦翩狨麸孱洵蝈盹翦痱镢邃躜瀛皓鲠祯濠Ⅲ弭翳腴祆轭镦翳蝈盹翦痱镢邃躜麸荐犰蹂井铒瞽铋轭溟汜翦翳狒翳蝈盹翦痱镢邃躜箬秕熹忮腴祆邃翳溴驷蹯舂铋轭溟汜翦翳轭鲥蝮瀹箦翩狨麸腴祆蜷洫鲠祯濠ㄤ彐躅汜祆蝈盹翦痱镢邃躜蜷钺礤蝈篝狎珞汜祆翳蝈盹翦痱镤邈躜鏖翳钺礤碱犴寰犷坚蜱缶狍翳狎珲礤铘鏖翳简殇狍蝈驽蝈钽瀹狍篚礤翳箴邈獒鲠蜷徕戾棂潋岘桢徜犷棂潋岘怙澌屮轶犷蝈箴邈糸鲥禊泔铘衢棂潋岘桢徜犷棂潋岘怙澌ㄡ篌弪ㄧ弭蝈盹翦痱镢邃躜钺礤┅ㄢ矧溴狨翳蝈徜蠛磲脲翳蝈徜戾è蜷洫蜷洎翳蝈徜徕戾灬礅溽ī戾è沲蝌孱舡翳蝈徜篝狎舡轭翦蝾犰蝓铘轫濯ㄧ弭轭翦蝾犰蝓瞽糸礤┅í狨麸腴祆蜷洫舂篝狎舡蜷蜷洫躅鏖钿痱雉邈ㄡ痧禊ㄧ弭蝈盹翦痱镢邃躜钺礤狎珞麒孱ㄡ豸锃孱洵蝈盹翦痱镢邃躜瀛皓鏖翳祜汜飙筱蝈孱祜汶āā瘐箬蜷筱蝈孱鲠蜷潴麸孱洎┅┅┅┅洪铋糸犰忾钿轭珞翳蝈徜轭轸獒飙忾钿轭珞侯犴钺礤┅ㄥ鲠飙麒孱ê泔眇殪瀛麸痨弼屐红镝洵麸痨弼屐哄邈豸濠ㄤ彐躅磲脲蝈盹翦痱镢邃躜瀛灬礅溽骢钽糸镱ㄡ蜱蹴孱趔怙澌⑩蹰熹翳蟓屮痱弩箝镱蝈痱弩孱翎糸镱骘翳灬礅溽骢钽糸镱麒殂汜忮汜祆邃骘翳溴骈铋糸镱镦蝈盹翦痱镢邃躜瀹翳轶栳钿戾翳泸遽糸镱镦翳脲狎珲礤铘螽戾舄è狎珲礤铘箴扉舡箦聃孱沐后痨轸箦聃孱沐Е脲狎珲礤铘螬簌礅镬祜镳蝈疱狒ū戾铉翳狎珲礤铘螬泔祆邈ㄧ孱簌㈥箫黝镡赍泗┅┅灬忮祗换秕麴豸泔溴麸忾钿坚蜱麸翳鲠祯轸栳轭缄箫黝镡戥鲠蚓磲脲牦秣瞽脲簌憝忾钿轭牦秣瞽镡戥鲠狎绌戾è牦秣瞽脲篝蜷铉滹黝汜箦篝蜷铉狎绌┅啜狎ㄡ钿ㄦ轭牦秣瞽脲牦秣詈脲黠蜾牦秣瞽镡戥鲠颟呼弩＇篝蜷铉僵牦秣詈鲠牦秣瞽镡戥鲠牦秣瞽脲┅┅换磲脲翳怙澌镦犰戾忾钿轭珞骘翳脲狎珲礤铘蟋翳雉桢蝮换栳钿戾轭翳狎珲礤铘翳灬礅溽骢钽糸镱徙沐痿磲脲牦秣瞽鲠飙戾舡忾钿轭珞ī祜镳骘簌轭簌礅镬骘狎珞轭蝈篝狎珲礤铘螬狃疱钿祜镳骘狎轭狎珞泔祆邈磲脲牦秣瞽脲簌憝忾钿轭簌狎绌┅┅啜灬礅溽ì括骈蝮狎珲礤铘螬荔礅镬螬括殒蝈篝狎珲礤铘螬啜戾磲脲牦秣瞽鲠飙戾舡忾钿轭珞棱镤┅怙澌┅┅┅ㄥ鲠飙麒孱ê泔眇殪瀛麸痨弼屐红镝洵麸痨弼屐哄邈豸濠ㄤ彐躅趄犷箪狒瀛蝈盹翦痱镢邃躜瀛钺礤钺礤Ⅳ蜥铙灬翦翳蝈盹翦痱镢邃躜钺礤碱犴寰麸翳蝈盹翦痱镢邃躜钺礤狍汜忮汜祆邃骝镯翳赆鲠筱蜷痿黠蜢洚ㄩㄡ钿簌礅镬钺礤铒箫礤＇祜麇颦汜箦篝蜷铉钺礤┅┅篝蜷铉滹黝汜箦篝蜷铉钺礤┅篝蜷铉钺礤┅┅ㄤ彐磲泸溴骈铄蝈盹翦痱镢邃躜钺礤é蝈篝狎珲礤铘螬怙澌怙澌溴骈铄蝈盹翦痱镢邃躜鏖翳碱犴寰狍翳钺礤麸忮汜祆邃犷坚蜱蹴孱趔狍翳狍篚礤狎珲礤铘螽殒碱犴寰轶簌礅镬鏖翳镱禊铒瞽祜麇颦汜箦汨狎徙翦蝮翳孱轸轶泔铞弪翦麸祜麇蜚狍瀹啜蝈玳篝弪蝈盹翦痱镢邃躜趄犷箪狒瀛蝈盹翦痱镢邃躜瀛钺礤钺礤磲脲蝈盹翦痱镢邃躜瀛灬礅溽骢钽糸镱狎珲礤铘怙澌┅┄溴骓徙蝻鏖翳弪蝻颦狍驷殪é怙澌怙澌㈠邈豸弩尖镤轭犷孱鲩蝻铐孱轭麒殂犰弪蝻蝮狎汜翥桢犷箦铘狍礤篌徵鏖翳豉疱堍驷殪堍麸翳躞弪啜栳钿戾颦汜箦痱镧棱镤ㄥ蝌矧ㄥ蝌礤篌徵㈡衢膦ㄦ矧磲铋立弪颟┅┅ㄤ彐躅蜷洵徙糸鲥蜷镳糸镱犰睐徙糸鲥蜷潴筱蝈孱鲠蜷潴┅Ⅱ弭躜铙铒瞽铋殒简殇轶徙糸鲥骘翳沲蝌孱躞弪怡躞镦翳鲠蜷徕戾睐徙糸鲥蜷潴翳沲蝌孱綮徙糸鲥蜷潴汜忮秭弪蜷滗孱★铎躞麒孱秕腩秣麒狒秕蝈滹轭纭矧篝蜷铉蜷ㄦ轭蜷睐徙糸鲥蜷潴呼弩＇篝蜷铉僵┅ㄤ彐躅篝狎舡蜷蜷洎Ⅲ弭简殇麸忮徙糸鲥鏖翳祜汜飙筱蝈孱祜汶ā躅戾篌蜷洵徙糸鲥蜷洎ā瘐箬蜷筱蝈孱鲠蜷潴┅┅┅ㄤ彐躅蝈盹鲥蜷蜷洎Ⅱ屙秭弩翳简殇骝镯翳扉篝镦徙糸鲥蜷潴鏖翳祜汜飙筱蝈孱祜汶āāㄡ戾犷潋獒候屙秭彐筱蝈孱鲠蜷潴蜷呼弩＇篝蜷铉僵┅ㄤ彐躅轭徙糸鲥蝈盹翦痱镢邃躜瀛īⅡ弭躜铙铒瞽铋殒麇狎沲蝌孱綮轭蝈盹翦痱镢邃躜鏖翳犷徙糸鲥蜷洚ㄡ钿棂潋岘怙澌棂潋岘桢徜蜷洫蜷洵徙糸鲥蜷洫ㄨ潋岘桢徜徙糸鲥棂潋岘桢徜ㄨ潋岘怙澌徙糸鲥棂潋岘怙澌┅ㄤ彐躅徙糸鲥īⅡ弭躜铙铒瞽铋殒麇狎沲蝌孱綮轭犷徙糸鲥蝈盹翦痱镢邃躜瀹犰獒骘轭徙糸鲥蝈盹翦痱镢邃躜瀛甬ㄩ瞽徙糸鲥蝈盹翦痱镢邃躜瀛皓┄溴骢礤篌徵豉疱怙澌Ⅲ孱潴礤篌徵麸翳沆殄铘鏖翳祜汜飙筱蝈孱祜汶āㄩㄩ瞽徙糸鲥蝈盹翦痱镢邃躜瀛皓戾è礤篌徵牦秣詈铄鳝牦á豉疱豉疱á蜷洧蜷洫á糸礤ㄩ沲蝌孱舡翳蝈徜篝狎舡轭翦蝾犰蝓铘轫濯ㄣ镥蜚ǒōㄧ弭轭翦蝾犰蝓瞽糸礤沲蝌孱舡翳蝈徜篝狎舡轭翦蝾犰蝓铘轫濯轭翦蝾犰糸礤躅轸蟓疱颦箦泔钿ф祜狒㈤铈轭轸┅á怙澌怙澌┅┅ā瘐箬礤篌徵筱蝈孱鲠ы弩筢珏螬┅麽蝾汜瞌箦钿礤篌徵弩殒铒轭犷徙糸鲥蝈盹翦痱镢邃躜澧┅┅ㄤ彐躅驽翥璀犷洵沆遽颦礤篌徵弩ī㈡弭汨弩犷沆遽蝮翳礤篌徵弩轭翳磲殪怙鏖翳祜汜飙筱蝈孱祜汶ā戾礤篌徵弩睐徙糸鲥蜷潴ā换驽翥翳扉篝镦礤篌徵弩箦翩礤篌徵弩筱蝈孱鲠ы弩筢珏螬箦翩筱蝈孱鲠ы弩筢珏螬铋飑换泔蝌邈綮汨犷珏翳徙糸鲥蜷潴箦翩睐徙糸鲥蜷潴筱蝈孱鲠蜷潴┅戾è蜷潴麸孱筱蝈孱鲠蜷潴麸孱洎┅箦翩筱蝈孱鲠蜷潴蝈盹鲥殒灬礅溽蜷洎ㄦ轭蜷蜷潴麸孱呼弩＇篝蜷铉僵筱蝈孱鲠蜷潴┅┅箦翩筱蝈孱鲠蜷潴麸孱洎铋飑ㄤ屐弭瀛殒铒灬礅溽礤篌徵濠蜷洵徙糸鲥牦秣詈鲠礤篌徵Ⅱ殇睐徙糸鲥蜷潴┅蝈鲥蝮礤篌徵弩┅┅ㄤ彐磲泸鏖翳棂潋岘狨翳篝矧瀛祜汶é怙澌怙澌㈠邈豸弩尖镤轭犷孱鲩蝻铐孱轭麒殂棂潋岘狨翳篝矧濯轶祜汶邃啜怙蜾遽貘翳蝈徜蠛鏖翳祜汶桢熹í棂潋岘狨翳祜汶棱镤┅ㄤ彐躅篝矧瀛棂潋岘鲠扉溽糸镱箦篌轱瞽鲠扉溽糸镱Ⅲ麸蝈翳棂潋箦篌轱瞽鲠扉溽糸镱箫轸汜忮骘躅忉汶ㄡ篌弪舡棂潋岘箦篌轱瞽鲠扉溽糸镱箦篌轱瞽鲠扉溽糸镱ㄡ篌弪舡棂潋岘怙澌箦篌轱瞽鲠扉溽糸镱棂潋岘怙澌箦篌轱瞽鲠扉溽糸镱┅鏖翳棂潋岘狨翳篝矧瀛祜汶瘐箬箦篌轱瞽鲠扉溽糸镱ㄧ弭栳箬箦篌轱瞽鲠扉溽糸镱棂潋岘殇箦篌轱瞽鲠扉溽糸镱棂潋岘狨翳篝矧濯┅┅ㄤ彐躅蝈趄殄鲥棂潋岘鲠扉溽糸镱ㄨ潋岘殇Ⅱ弭躜铙犰棂潋箦篌轱瞽鲠蜢殇狒轱轭篝犷沐麒殂忮祜铉麸翳玳鲥棂潋岘殇ㄡ篌弪舡铒铄眇豉篝蜷铉棂潋岘殇鏖翳棂潋岘狨翳篝矧瀛祜汶ㄧ弭栳箬棂潋岘殇棂潋岘狨翳篝矧濯┅ㄤ彐躅蝈盹鲥棂潋岘鲠扉溽糸镱箦篌轱瞽鲠扉溽糸镱Ⅱ屙秭弩翳箦篌轱瞽鲠扉溽糸镱俭弩箝镱鲠扉溽糸镱骝镯翳腩秣鲠扉溽糸镱螽ㄡ篌弪舡棂潋岘箦篌轱瞽鲠扉溽糸镱箦篌轱瞽鲠扉溽糸镱鏖翳棂潋岘狨翳篝矧瀛祜汶换滹箫礤翳轭箜狎鏖翳泔躅翦轭翳棂潋岘怙澌桢蝈箫麇腩秣翳棂潋岘怙澌换箬秕熹忮翦蝽轭狒邃麸蝈龛狍箦篌轱瞽鲠扉溽糸镱棂潋岘殇箦篌轱瞽鲠扉溽糸镱棂潋岘狨翳篝矧濯┅┄溴骢孱篚蝈棂潋ī㈠铙躜弩翳棂潋轶箦躔翳轶礤犷翳狒徭翦翳轶骢钽糸镱屮邈豸轱詈棂潋岘桢徜轶怙躅麸翳棂潋岌桢徜棂潋岘怙澌轶怙躅麸翳棂潋岌怙澌棂潋岘狨翳篝矧濯泔铘衢铙犷狨翳孱糸汜糸镱骘泔铘轭蹂篝矧徵徭翦翳轶蝈聃弩艉翳躞弪栳堍棂潋彳泔镫殄麒殂扉铍麸翳轶箦篌轱町躅戾篌ㄤ轶泔鲥颦棂潋岍戾è棂潋岘怙澌ㄢ蹰熹徙糸鲥棂潋岘怙澌┅ㄥ铙躜瀛棂潋岘桢徜棂潋岘怙澌ㄢ蹰熹徙糸鲥狨翳孱糸汜糸镱棂潋岘怙澌┅┅ㄤ彐躅怩殪洵徙糸鲥棂潋岘怙澌ī⑩蹰熹铄棂潋岘怙澌犷箦趔轸狍翳沲蝌孱棂潋岘怙澌箦翩棂潋岘怙澌磲脲棂潋岘怙澌┅ㄤ彐躅怩殪洵徙糸鲥狨翳孱糸汜糸镱ㄨ潋岘怙澌⑩蹰熹铄狨翳孱糸汜糸镱麒殂殇孱糸骈弩棂潋岘怙澌犷篝矧弩轸轭翳铄沐篌狎篝蝓泗躜弩戾è殇螳ㄧ孱弪狒瀛殇┅┅篝矧瀛棂潋岘鲠扉溽糸镱磲脲箦篌轱瞽鲠扉溽糸镱棂潋岘怙澌殇┅换翳轶泔镫殄轶铄鲥躔溽翦洚轸箬秕熹忮躔溽翦弼弪箫镦翦箫麇汜换脲屦翳箦篌轱犰轹ㄨ躅汨孱麸雉后弭泔镫殄㈣潋幄忽犰蹂殇鸿趑瓠镱禊哄痖蝈ǐㄧ弭躅轹弪筢飙糸礤í栋栋泊嘲┅┅ㄤ彐躅溟筱秭弪棂潋ī溟筱秭弪棂潋蝈趱蝾铒瞽铋殒麇栳箦篌轱瞽泔镫殄翳蝻蹒麒殂麇泔蹯骈钿棂潋箦篌轱瞽鲠扉溽糸镱麒殂轶鲠扉骘秕沲蝌孱箦篌轱町翳轶骢钽糸镱箦趔躔犰箴邈獒鲠蜷徕戾骘翳棂潋麸忮栳痧轸犰箫麸蹉桢翳棂潋岘怙澌犷翳蝈戾鲠铘棂潋岘桢徜箫麇蝈徙糸鲥麒孱ㄤ轶泔鲥颦棂潋岘怙澌ㄥ铙躜瀛棂潋岘桢徜棂潋岘怙澌┅ㄤ彐躅溟筱秭弪棂潋岘怙澌īⅡ弭躜铙铒瞽铋殒麇栳箦篌轱瞽泔镫殄翳蝻蹒麒殂麇泔蹯骈钿棂潋箦篌轱瞽鲠扉溽糸镱麒殂轶鲠扉骘秕沲蝌孱箦篌轱町翳轶骢钽糸镱箦趔躔犰箴邈獒鲠蜷徕戾骘翳棂潋岘怙澌怩戾狯弩翳棂潋岘桢徜骘犷雉桢箫祯糸镱麸骈珲蝈秕舢ㄥ筱ㄞ箦翩棂潋岘怙澌箦篌轱瞽鲠扉溽糸镱棂潋岘怙澌戾è泔镫殄ㄞㄨ躅汨孱麸雉恒镲腴瀛轭㈣潋幄┅┅ㄞㄦ轭洵殒蜚躜蝙＇鲠扉洵箦篌轱瞽泔镫殄蝈趄殄鲥棂潋岘鲠扉溽糸镱泔镫殄┅┅┅麸蹉棂潋岘怙澌棂潋岘怙澌┅ㄤ彐躅孱篚蝈棂潋岘桢徜ㄨ潋岘怙澌㈠铙躜弩翳棂潋岘桢徜屮轶趔犷轶箦轭翳鲠蜷徕戾棂潋岘桢徜狍篚礤棂潋岘怙澌轶箦舢蝈趱蝾翳沲蝌孱棂潋岘桢徜眭祠轲戾鲠祯瀛忾钿ㄨ栝铄邃麸箦钿梃殇皓ㄥ铙躜瀛梃殇戾è棂潋岘桢徜ㄦ轭梃殇ㄨ潋岘怙澌桢徜棂潋岘怙澌呼弩＇篝蜷铉弘妁＇棂潋岘桢徜殇┅ㄩ棂潋岘桢徜痱镧麸蹉棂潋岘桢徜箦翩棂潋岘桢徜棂潋岘桢徜┅戾è铄鳝桢徜磲脲棂潋岘桢徜洪梃殇┅箦翩棂潋岘桢徜铄鳝桢徜瘐箬铄鳝桢徜ㄨ潋岘怙澌桢徜棂潋岘怙澌┅┅麒孱铄邃麸箦钿梃殇箦钿沲蝌孱舡梃殇┅棂潋岘桢徜ㄤ彐躅孱篚蝈梃殇īⅡ弭躜铙翳梃殇殒镱麽玳鲥狍珏舡鲠蜷徕戾矧泸遽翦铄梃殇滹弩铒舄瘐翳梃殇镱翳礤篌徵篝徙氘蝈趱蝾鲠祯弩梃殇铄黟┊殒铄黟轶衄礤篌徵箬秕熹忮箦铘麸翳沆殄铘箦箦钿沲蝌孱舡梃殇┅箫翳沆殄铘腩秣翳梃殇戾蝈箦钿皓鲠祯弩矧戾è箴邈獒棂潋岘桢徜殇┅箴邈獒飑戾è珏ㄨ躅汨孱麸雉虹弭疳蜥礤翦㈣栝洧┅箦翩棂潋岘桢徜殇珏舂珏舂戾è铄螳ㄧ孱弪狒瀛殇┅┅箦翩棂潋岘桢徜殇铄鳗箦翩蝈箦钿舂铄鳗蝈箦钿皓┅ㄤ彐躅箦钿沲蝌孱舡梃殇īⅢ孱潴翳沲蝌孱梃殇麸翳沆殄铘怡躞轭翳泔蝌邈轭翦蜚镯礤篌徵瀹蝈聃轵弩翳狒棂潋岘桢徜犷棂潋岘桢徜殇狎箦泔蝌邈綮ㄡ篌弪舡棂潋岘桢徜棂潋岘桢徜ㄡ篌弪舡铒铄眇豉篝蜷铉棂潋岘桢徜殇戾è蜷洫┅礤篌徵㈣栝洧棂潋岘桢徜殇┅┄溴骟趄蹉棂潋岘怙澌ㄤ狒磲脲脲鲠祯瀛篝矧濠ㄡ糸礤ㄧ弭躅轹弪筢飙糸礤┅ㄨ遽潴铋飑ㄧ悱汜祆忉汶铋飑ㄧ狎忉珏泔祆邈翦洵怙澌铋飑ㄤ彐礤翳镤麸蹉è棂潋棂潋岘怙澌┅箦翩ㄨ潋岘怙澌狒轫棂潋岍ㄧ弭躅轹弪筢飙糸礤┅ㄤ彐躅玢棂潋岘怙澌ㄨ潋岘怙澌㈢狎忉珏泔祆邈趔棂潋岘怙澌ㄤ镬轶ㄨ遽ㄨ潋岘怙澌桢徜棂潋岘怙澌┅ㄧ悱棂潋岘桢徜桢徜┅ㄤ镬轶ㄣ犰焘徙ㄨ潋岘怙澌玢汜祆忉汶棂潋岘怙澌┅ㄦ躅汜祆汜祆忉汶┅箦翩ㄨ潋岘怙澌玑蜮徵瀛泔祆邈翦洵怙澌棂潋岘怙澌舂ㄤ彐躅箦篌轱瞽鲠脲镳糸镱犰箦篌轱棂潋岘怙澌┅Ⅱ弭躜铙翳鲠祯镦茧妁麒殂忮祜铉麸俭弩箝镱粳矧铋殒轸溟漕屮轶舢翳箦泔钿鲠祯轶铒瞽铋殒茧妁麽骘躅轭俭弩箝镱井膂篝矧瀛蝈徜脲ㄨ潋岘怙澌溽翎箦篌轱瞟┅ㄤ彐躅箦翩箦篌轱瞽鲠颟鲠祯脲镳糸镱犰箦篌轱棂潋岘怙澌┅Ⅲ弭翳鲠祯镦脲麒殂忮祜铉麸俭弩箝镱麸荐犰蹂井箦翩膂篝矧瀛蝈徜脲ㄨ潋岘怙澌溽翎箦篌轱瞟鲠祯濠ㄤ彐躅狒翎汨桢徜ㄨ潋岘怙澌棂潋岘桢徜⑨趑徙桢艰潋岘桢徜麸艰潋岘怙澌劲ㄡ篌弪舡棂潋岘怙澌棂潋岘怙澌ㄡ篌弪舡棂潋岘桢徜棂潋岘桢徜瘐箬棂潋岘桢徜ㄨ潋岘怙澌桢徜棂潋岘怙澌┅ㄤ彐躅棂潋岘怙澌徙糸鲥ㄨ潋岍Ⅱ弭躜铙铒瞽铋殒翳艰潋峋栳箢忮孱麸蹉桢骘麸祜铉镦糸礤ㄡ钿铒ㄨ潋岘怙澌玑蜮徵瀛泔祆邈翦洵怙澌棂潋岍ǐㄨ潋岘怙澌狒轫棂潋岍棂潋岘怙澌糸礤秕舄ㄧ弭躅轹弪筢飙糸礤┅┅ㄤ彐躅徜洵箦篌轱瞽玢汜祆忉汶ㄦ躅泗轱镳糸镱犰箦篌轱棂潋岘怙澌┅⑨滗兼躅泗轱罹麸翳扉篝镦骢钽糸镱麸汜祆镱玑蜮徵泔祆邈糸镱镦俭弩箝镱井鏖翳箦篌轱瞽溻祜汶ī瘐箬骢钽糸镱ㄨ潋岘怙澌玢汜祆忉汶箦篌轱瞟┅ㄤ彐躅蝈盹鲥箦篌轱瞽玢汜祆忉汶ㄦ躅泗轱镳糸镱犰箦篌轱棂潋岘怙澌┅Ⅱ屙秭弩兼躅泗轱罹骝镯翳扉篝镦骢钽糸镱麸汜祆镱翳玑蜮徵泔祆邈糸镱镦俭弩箝镱井鏖翳箦篌轱瞽溻祜汶ī蝈盹鲥ㄨ潋岘怙澌玢汜祆忉汶箦篌轱瞟骢钽糸镱┅ㄤ彐篝蝓泗棂潋岘桢徜ㄩ铋飑ㄤ狒磲脲脲鲠祯瀛篝矧濠ㄡ糸礤ㄧ弭躅轹弪筢飙糸礤┅ㄧ悱汜祆忉汶铋飑ㄧ狎忉珏泔祆邈翦洵怙澌铋飑ㄤ彐礤翳镤麸蹉è棂潋棂潋岘桢徜┅箦翩ㄨ潋岘桢徜狒轫棂潋岍ㄧ弭躅轹弪筢飙糸礤┅ㄤ彐躅玢棂潋岘桢徜ㄨ潋岘桢徜㈢狎忉珏泔祆邈趔棂潋岘桢徜ㄤ镬轶ㄣ犰焘徙ㄨ潋岘桢徜玢汜祆忉汶棂潋岘桢徜┅ㄦ躅汜祆汜祆忉汶┅箦翩ㄨ潋岘桢徜玑蜮徵瀛泔祆邈翦洵怙澌棂潋岘桢徜舂ㄤ彐躅筱蝈孱鲠脲镳糸镱犰筱蝈孱棂潋岘桢徜┅Ⅱ弭躜铙翳鲠祯镦茧妁麒殂忮祜铉麸俭泸邋罹矧铋殒轸溟漕屮轶舢翳箦泔钿鲠祯轶铒瞽铋殒茧妁麽骘躅轭俭泸邋罹膂篝矧瀛蝈徜脲ㄨ潋岘桢徜溽翎筱蝈孱┅ㄤ彐躅箦翩筱蝈孱鲠颟鲠祯脲镳糸镱犰筱蝈孱棂潋岘桢徜┅Ⅲ弭翳鲠祯镦脲麒殂忮祜铉麸俭泸邋罹麸荐犰蹂井箦翩膂篝矧瀛蝈徜脲ㄨ潋岘桢徜溽翎筱蝈孱┅鲠祯濠ㄤ彐躅棂潋岘桢徜徙糸鲥ㄨ潋岍Ⅱ弭躜铙铒瞽铋殒翳艰潋峋栳箢忮孱麸蹉桢骘麸祜铉镦糸礤ㄡ钿铒ㄨ潋岘桢徜玑蜮徵瀛泔祆邈翦洵怙澌棂潋岍ǐㄨ潋岘桢徜狒轫棂潋岍棂潋岘桢徜糸礤秕舄ㄧ弭躅轹弪筢飙糸礤┅┅ㄤ彐躅徜洵筱蝈孱玢汜祆忉汶ㄦ躅泗轱镳糸镱犰筱蝈孱棂潋岘桢徜┅⑨滗兼躅泗轱罹麸翳扉篝镦骢钽糸镱麸汜祆镱玑蜮徵泔祆邈糸镱镦俭泸邋罹鏖翳箦篌轱瞽溻祜汶ī瘐箬骢钽糸镱ㄨ潋岘怙澌玢汜祆忉汶箦篌轱瞟┅ㄤ彐躅蝈盹鲥筱蝈孱玢汜祆忉汶ㄦ躅泗轱镳糸镱犰筱蝈孱棂潋岘桢徜┅Ⅱ屙秭弩兼躅泗轱罹骝镯翳扉篝镦骢钽糸镱麸汜祆镱翳玑蜮徵泔祆邈糸镱镦俭泸邋罹鏖翳箦篌轱瞽溻祜汶ī蝈盹鲥ㄨ潋岘怙澌玢汜祆忉汶箦篌轱瞟骢钽糸镱┅ㄤ彐篝蝓泗箦篌轱瞽鲠扉溽糸镱ê泔铙趄蹉麸黼箦篌轱瞽鲠扉溽糸镱┅ㄨ潋岘殇呼疱篝蜷铉ㄨ矬呼疱篝蜷铉躞弪徵孱呼疱篝蜷铉ㄨ潋岘怙澌铋呼疱矧棂潋岘怙澌铛祆┅ㄤ彐躅鲠扉洵箦篌轱瞽箦篌轱瞽鲠扉溽糸镱泔镫殄殇孱糸骈弪Ⅵ犰殇狒弩翳箦篌轱瞽鲠扉溽糸镱骘翳沲蝌孱蝈聃弩簪ㄡ钿篝蜷铉泔镫殄殇孱糸骈弪箦篌轱瞽鲠扉溽糸镱棂潋岘殇箦篌轱瞽鲠扉溽糸镱┅篝蜷铉ㄨ躅汨孱麸雉鸿矬舂箦篌轱瞽鲠扉溽糸镱栾篝箦篌轱瞽鲠扉溽糸镱┅篝蜷铉ㄨ躅汨孱麸雉乎箦颦徵孱舂箦篌轱瞽鲠扉溽糸镱躞弪徵孱箦篌轱瞽鲠扉溽糸镱┅┅ㄤ彐躅磲脲箦篌轱瞽鲠扉溽糸镱ㄨ潋岘怙澌泔镫殄殇孱糸骈弪泔铙趄蹉趔铄箦篌轱瞽鲠扉溽糸镱镡赍泗骘翳沲蝌孱箦篌轱町黼箦篌轱瞽鲠扉溽糸镱鸿潋岘殇泔镫殄殇孱糸骈弪鸿潋岘怙澌棂潋岘怙澌鸿矬ㄨ躅汨孱麸雉鸿矬舂乎箦颦徵孱ㄨ躅汨孱麸雉乎箦颦徵孱舂┅ㄨ躅汨孱麸雉轰彐轭瀛遽簌栳钿戾翎祀乎蜷翎祀īㄩ瞽轭翦蜚镯箦篌轱ㄥ铙躜瀛棂潋岍箦翩ㄨ躅汨孱麸雉恒镱翦铘豉疱⑨痧扉汜糸镱牦镱痖铉祜珑轭绌戾è镳孱ㄨ躅汨孱麸雉吼狎犴弭弪镳孱┅ㄣ祜箦ㄨ躅汨孱麸雉吼狎犴弭弪沆矬澧┅麒孱镳孱戾è镳孱蝈聃弩趔牦秣詈疳蝮镳孱┅蝈聃弩舡祜珑轭镳孱蝈聃弩趔ㄤ镬轶蝈聃弩镳孱蝈聃弩趔疱蜴矧憝轭翦蜚镯蝈聃弩蝈聃弩舂┅换埯蜷洮礤翳镤狎珞麒孱沆矬戾è沆矬瀛蝈聃弩趔牦秣詈疳蝮沆矬濠┅ㄣ祜箦蝈聃弩舡祜珑轭沆矬瀛蝈聃弩趔ㄤ镬轶蜷沆矬瀛蝈聃弩趔疱蜴矧憝沆矬瀛蝈聃弩蜷洎┅┅换蜷潴戾è礤篌徵弩ㄦ弭汨犷洵沆遽颦礤篌徵弩┅蝈箴镱箦祜珑轭礤篌徵弩牦秣詈麸牦镱礤篌徵弩┅┅ㄤ彐疳蜥礤翦祜绛篝蝈犴祜汶ㄢ矧溴狨翳蝈徜蠛磲脲祜汶㈤铘弪泔憝祜绛篝蝈犴祜汶Ⅳ栝祜汶箬秕熹忮桢熹麒孱黩轸轭麸翳祜篝蝈犴ㄤ彐躅磲脲祜绛礤篌徵瀛篝蜷铉豉疱蝈篝狎珞泔铙趄蹉趔祜礤篌徵篝蜷铉骘豉疱犷翳骘祆秣邃狎珲礤铘螽鏖翳秕麴豸麸篝蜷铉秕舂黩轸啜豉疱ㄧ弭躅轹弪筢飙糸礤ㄨ躅汨孱麸雉恒镲腴瀛轭㈣潋幄ㄨ潋岘桢徜殇棂潋岘桢徜泪蜱螬后趄遽秕候遽溽忪候殓梏磲蜱轭备癌┅ㄤ彐磲泸磲忮祜绛蝈聃弩é怙澌怙澌㈠邈豸弩尖镤轭犷孱鲩蝻铐孱麒弪翳检镧骢钽糸镱轶溴骈铄殒祜绛蝈聃弩舡篝蝈犴轶铒瞽铋飚翳检镧骢钽糸镱翎脲翳豉疱镦泔铘孱麸祜绗骘祆秣邃怡翳泔铘孱轸箦戽怡蝈篝犷轸箴扉沐棂潋岘怙澌殇棂潋岘桢徜殇轭翳狒扉篝ㄡ戾犷潋獒瑚轸璀珏铙眢篝蜷铉蟓鲠颟啜麒孱祜绛蝈聃弩舡篝蝈犴戾ì篝蜷铉蟓鲠颟ㄦ戾è祜绐豉疱蝈篝狎珞瘐箬痱轭悱麸篝蜷铉ㄡ痧禊＇磲脲祜绛礤篌徵瀛篝蜷铉豉疱狎珞┅篝蜷铉蟓鲠颟┅棱镤麒孱篝蜷铉蟓鲠ㄢ矧溴狨翳蝈徜蠛鏖翳祜汶桢熹í祜绛篝蝈犴祜汶ㄦ矧磲祜绛蝈聃弩舡篝蝈犴窿" (reverse ,strings-var)))))))))

(defun ping-logging ()
  "handles the logging of the ping request"
  (maybe-log-request
    (log* :ping (hunchentoot:real-remote-addr))))

(defun request-logging (requests)
  "handles the logging of the new requests"
  (maybe-log-request
    (dolist (r requests)
      (apply #'log* :req
             (handler-case (jsown:val r "rid")
               (error () nil))
             (handler-case (jsown:val r "name")
               (error () nil))
             (handler-case (jsown:val r "args")
               (error () nil))))))

(defun close-request-logging (requests)
  "handles the logging of close requests"
  (maybe-log-request
    (dolist (rid requests)
      (log* :close rid))))

(defun response-logging (responses)
  "handles the logging of the responses"
  (maybe-log-request
    (dolist (r responses)
      (log* :res
            (jsown:val r "rid")
            (jsown:val r "time")
            (jsown:val r "type")
            (jsown:val r "body")))))


(defun hash-keys (hash)
  "returns a list of all hash-keys in <hash>"
  (loop for k being the hash-keys of hash collect k))

(defun gc-hydra-bodies ()
  "garbage collect the head hydras.  this removes the session-validation objects
  and removes the head heads."
  (bordeaux-threads:with-lock-held (*hydra-auth-lock*)
    (loop for k in (hash-keys *hydra-auth-store*)
       for validations =
         (remove-if-not (lambda (session-validation)
                          (let* ((hydra-body (session-validation-hydra-body
                                              session-validation))
                                 (activep (hydra-body-active-p hydra-body)))
                            (unless activep
                              ;; we need to decide what the throw away at this
                              ;;  time to ensure we don't forget to gc
                              (gc-hydra-body hydra-body))
                            activep))
                        (gethash k *hydra-auth-store*))
       if validations
       do 
         (setf (gethash k *hydra-auth-store*)
               validations)
         (mapcar (compose #'gc-hydra-heads #'session-validation-hydra-body)
                 validations)
       else
       do
         (remhash k *hydra-auth-store*))))

(defun gc-hydra-heads (hydra-body)
  "detaches the dead heads from <hydra-body>."
  ;;---! assumes hydra-body is locked by us
  (assert-hydra-body hydra-body)
  (let* ((new-heads (remove-if-not (lambda (head)
                                     (let ((activep (hydra-head-active-p head)))
                                       ;; we need to inline the garbage collection
                                       ;;  otherwise we may miss one somehow (though unlikely)
                                       (unless activep
                                         (gc-hydra-head head))
                                       activep))
                                   (hydra-body-heads hydra-body))))
    (setf (hydra-body-heads hydra-body)
          new-heads)))

(bordeaux-threads:make-thread
 (let ((store *hydra-auth-store*)
       (lock *hydra-auth-lock*))
  (lambda ()
    (let ((*hydra-auth-store* store)
          (*hydra-auth-lock* lock))
      (loop do
           (sleep 1800) ;; we run every 30 minutes
           (gc-hydra-bodies)))))
 :name "hydras garbage collection thread")

(defun perform-intercom-request (jsown-request)
  "performs an intercom request as described by <jsown-request>."
  (apply #'call-remote-procedure
         (jsown:val jsown-request "rid")
         (jsown:val jsown-request "name")
         (jsown:val jsown-request "args")))(defun perform-close-request (rid)
  "closes the request for the rid."
  (remove-rid rid))
